/*
 * Copyright 2010 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
 import org.springframework.build.Version

// -----------------------------------------------------------------------------
// Main gradle build file for Spring Social
// @author Chris Beams
// @author Craig Walls
// -----------------------------------------------------------------------------

// -----------------------------------------------------------------------------
// Configuration for the root project
// -----------------------------------------------------------------------------
description = 'Spring Social'
abbreviation = 'SOCIAL'

apply plugin: 'base'
apply plugin: 'idea'

def buildSrcDir = "$rootDir/buildSrc"
apply from: "$buildSrcDir/wrapper.gradle"
apply from: "$buildSrcDir/maven-root-pom.gradle"

// -----------------------------------------------------------------------------
// Configuration for all projects including this one (the root project)
//
// @see settings.gradle for list of all subprojects
// -----------------------------------------------------------------------------
allprojects {
    // group will translate to groupId during pom generation and deployment
    group = 'org.springframework.social'

    // version will be used in maven pom generation as well as determining
    // where artifacts should be deployed, based on release type of snapshot,
    // milestone or release.
    // @see org.springframework.build.Version under buildSrc/ for more info
    // @see gradle.properties for the declaration of this property.
    version = new Version(springSocialVersion)

    // default set of maven repositories to be used when resolving dependencies
    repositories {
        mavenRepo urls: 'http://maven.springframework.org/release'
        mavenRepo urls: 'http://maven.springframework.org/milestone'
        mavenRepo urls: 'http://maven.springframework.org/snapshot'
        mavenCentral()
    }
}


// -----------------------------------------------------------------------------
// Create collections of subprojects - each will receive their own configuration
// - all subprojects that start with spring-social-* are 'java projects'
// - documentation-related subprojects are not collected here
//
// @see configure(*) sections below
// -----------------------------------------------------------------------------

javaprojects = subprojects.findAll { project ->
    project.path.startsWith(':spring-social-')
}

// -----------------------------------------------------------------------------
// Configuration for all java subprojects
// -----------------------------------------------------------------------------
configure(javaprojects) {

    apply plugin: 'java' // tasks for conventional java lifecycle
    apply plugin: 'maven' // `gradle install` to push jars to local .m2 cache
    apply plugin: 'eclipse' // `gradle eclipse` to generate .classpath/.project
    apply plugin: 'idea' // `gradle idea` to generate .ipr/.iml

    // set up dedicated directories for jars and source jars.
    // this makes it easier when putting together the distribution
    libsBinDir = new File(libsDir, 'bin')
    libsSrcDir = new File(libsDir, 'src')

    // add tasks for creating source jars and generating poms etc
    apply from: "$buildSrcDir/maven-deployment.gradle"

    // add tasks for finding and publishing .xsd files
    apply from: "$buildSrcDir/schema-publication.gradle"

    commonsHttpClientVersion = '3.1'
    jacksonVersion = '1.5.3'
    jodaVersion = '1.6'
    junitVersion = '4.7'
    mockitoVersion = '1.8.5'
    servletApiVersion = '2.5'
    springVersion = '3.0.5.RELEASE'
    springSecurityVersion = '3.1.0.CI-SNAPSHOT'

    sourceSets {
        test {
            resources {
                srcDirs = ['src/test/resources', 'src/test/java']
            }
        }
    }

    // 'provided' dependencies should be transitive (e.g. servlet-api)
    configurations {
        provided { transitive = false }
    }

    sourceSets.main.compileClasspath += configurations.provided
    sourceSets.test.compileClasspath += configurations.provided
    sourceSets.test.runtimeClasspath += configurations.provided

    // dependencies that are common across all java projects
    dependencies {
        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-all:$mockitoVersion"
        testCompile "org.springframework:spring-test:$springVersion"
    }

    // enable all compiler warnings (GRADLE-1077)
    [compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:all']

    // generate .classpath files without GRADLE_CACHE variable (GRADLE-1079)
    eclipseClasspath.variables = [:]
}


// -----------------------------------------------------------------------------
// Configuration for each individual core java subproject
//
// @see configure(javaprojects) above for general config
// -----------------------------------------------------------------------------

project('spring-social-client') {
    description = 'Service Provider Framework'
    dependencies {
        compile "org.springframework:spring-jdbc:$springVersion"
        compile "org.springframework:spring-web:$springVersion"
		compile ("org.springframework.security:spring-security-crypto:$springSecurityVersion") { provided = true }
        compile "commons-codec:commons-codec:1.4"
        testCompile "com.h2database:h2:1.2.143"
    }
}

project('spring-social-facebook') {
    description = 'Facebook Service Provider'
    dependencies {
        compile "commons-httpclient:commons-httpclient:$commonsHttpClientVersion"
        compile ("javax.servlet:servlet-api:$servletApiVersion") { provided = true }
        compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion"
        compile project(':spring-social-client')
        compile project(':spring-social-webmvc')
    }
}

project('spring-social-github') {
    description = 'Github Service Provider'
    dependencies {
        compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion"
        compile project(':spring-social-client')
    }
}

project('spring-social-gowalla') {
    description = 'Gowalla Service Provider'
    dependencies {
        compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion"
        compile project(':spring-social-client')
    }
}

project('spring-social-linkedin') {
    description = 'LinkedIn Service Provider'
    dependencies {
        compile "commons-httpclient:commons-httpclient:$commonsHttpClientVersion"
        compile project(':spring-social-client')
    }
}

project('spring-social-tripit') {
    description = 'TripIt Service Provider'
    dependencies {
        compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion"
        compile project(':spring-social-client')
    }
}

project('spring-social-twitter') {
    description = 'Twitter Service Provider'
    dependencies {
        compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion"
        compile project(':spring-social-client')
    }
}

project('spring-social-webmvc') {
    description = 'Spring MVC Integration'
    dependencies {
        compile "org.springframework:spring-web:$springVersion"
        compile project(':spring-social-client')
    }
}

project('spring-social-config') {
    description = 'Configuration Namespace'
    dependencies {
        compile "org.springframework:spring-beans:$springVersion"
        compile project(':spring-social-client')
        compile project(':spring-social-facebook') { optional = true }
        compile project(':spring-social-github') { optional = true }
        compile project(':spring-social-gowalla') { optional = true }
        compile project(':spring-social-linkedin') { optional = true }
        compile project(':spring-social-tripit') { optional = true }
        compile project(':spring-social-twitter') { optional = true }
        testCompile "com.h2database:h2:1.2.143"
    }
}

// -----------------------------------------------------------------------------
// Configuration for the docs subproject
// -----------------------------------------------------------------------------
project('docs') {
    apply from: "$buildSrcDir/docs.gradle"
}

apply from: "$rootDir/gradle/dist.gradle"
apply from: "${rootDir}/gradle/checks.gradle"
